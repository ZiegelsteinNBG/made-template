pipeline RhoenEnergiePipeline{
    RhoenEnergieExtractor
        -> RhoenEnergieZipArchiveInterpreter
        -> RhoenEnergieFilePicker
        -> RhoenEnergieTextFileInterpreter
        -> RhoenEnergieCSVExtractor
        -> RhoenEnergieTableInterpreter
        -> RhoenEnergieSQLiteLoader;

    block RhoenEnergieExtractor oftype HttpExtractor{ url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip"; }
    block RhoenEnergieZipArchiveInterpreter oftype ArchiveInterpreter { archiveType: "zip";}
    block RhoenEnergieFilePicker oftype FilePicker { path: "./stops.txt";}
    block RhoenEnergieTextFileInterpreter oftype TextFileInterpreter { encoding: "utf8"; }
    block RhoenEnergieCSVExtractor oftype CSVInterpreter{
        delimiter: ","; 
        enclosing: '"'; 
    }
    block RhoenEnergieTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype coordinate,
            "stop_lon" oftype coordinate,
            "zone_id" oftype zoneID
        ];
    }
    valuetype zoneID oftype integer { constraints: [ zoneIDConstraint ]; }
    constraint zoneIDConstraint on integer: value == 1925;
    valuetype coordinate oftype decimal { constraints: [coordinateConstraint]; }
    constraint coordinateConstraint oftype RangeConstraint {
        lowerBound: -90;
        upperBound: 90;
        lowerBoundInclusive: true;
        upperBoundInclusive: true;
    }
    block RhoenEnergieSQLiteLoader oftype SQLiteLoader {
        table: "stops"; 
        file: "./gtfs.sqlite";
    }

}